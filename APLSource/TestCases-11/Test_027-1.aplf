 R←Test_027(stopFlag batchFlag);list;types;⎕TRAP
 ⍝ Exercise `##.WinSys.GetDriveAndType`
 R←∆Failed
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')

 Match←{(∧/⍺∊⍵)∧(∧/⍵∊⍺)}

⍝ Tests...
 list←##.WinSys.GetDriveAndType

 →FailsIf~0<⍴list[;0]
 →FailsIf~∧/(↑¨list[;0])∊⎕A
 →FailsIf(,':')≢,∪1⊃¨list[;0]
 →FailsIf(,'\')≢,∪2⊃¨list[;0]
 →FailsIf(,3)≢∪↑∘⍴¨list[;0]
 →FailsIf~∧/(∪list[;1])∊⍳9

 ⍝ The following tests depend on local specialties - see "Initial"
 :If ~0∊⍴#.INI.FixedDrives
     →PassesIf(,#.INI.FixedDrives)Match↑¨(list[;2]≡¨⊂'Fixed')/list[;0]
 :EndIf
 :If ~0∊⍴#.INI.NetworkDrives
     →PassesIf(,#.INI.NetworkDrives)Match↑¨(list[;2]≡¨⊂'Remote')/list[;0]
 :EndIf
 :If ~0∊⍴#.INI.CDRomDrives
     →PassesIf(,#.INI.CDRomDrives)Match↑¨(list[;2]≡¨⊂'CD-ROM')/list[;0]
 :EndIf

 types←'Invalid Path' 'Removable' 'Fixed' 'Remote' 'CD-ROM' 'Ram-Disk'
 →FailsIf~∧/(∪list[;2])∊types

 R←∆OK
